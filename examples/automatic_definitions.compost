mod Point
    class
        x: Int
        y: Int
    traits
        X: Int
        Y: Int
    defs
        X: x
        Y: y
        String: .X.String + String(value: ', ') + .Y.String

mod Rectangle
    traits
        # A 'Rectangle' must have definitions for these traits.
        TopLeft: Point
        BottomRight: Point
        Width: Int
        Height: Int
    defs
        # Some automatic definitions based on other traits we have.
        TopLeft
            Point
                x: .BottomRight.X - .Width
                y: .BottomRight.Y - .Height
        BottomRight
            Point
                x: .TopLeft.X + .Width
                y: .TopLeft.Y + .Height
        Width: .BottomRight.X - .TopLeft.X
        Height: .BottomRight.Y - .TopLeft.Y

# A class that implements 'Rectangle', constructed using a point an size.
mod RectangleBySize
    class
        topLeft: Point
        width: Int
        height: Int
    defs
        Rectangle\TopLeft: topLeft
        Rectangle\Width: width
        Rectangle\Height: height
        # BottomRight is automatically defined for this class using the definition on the Rectangle module.

# A class that implements 'Rectangle', constructed using two points.
mod RectangleByPoints
    class
        topLeft: Point
        bottomRight: Point
    defs
        Rectangle\TopLeft: topLeft
        Rectangle\BottomRight: bottomRight
        # Width and Height are automatically defined for this class using the definitions on the Rectangle module.

lets
    # Rectangle is the type of this constant.
    # RectangleBySize implements the Rectangle type because it defines all traits of Rectangle.
    A: Rectangle
        RectangleBySize
            topLeft
                Point
                    x: Int(value: 10)
                    y: Int(value: 5)
            width: Int(value: 20)
            height: Int(value: 10)

    # RectangleByPoints implements the Rectangle type because it defines all traits of Rectangle.
    B: Rectangle
        RectangleByPoints
            topLeft
                Point
                    x: Int(value: 10)
                    y: Int(value: 5)
            bottomRight
                Point
                    x: Int(value: 15)
                    y: Int(value: 15)

    # The following values are calculated using automatic definitions from the Rectangle module.
    Main: String
        String(value: 'BottomRight of A: ') + A.BottomRight.String
        + String(value: '. Width and Height of B: ') + B.Width.String
        + String(value: ', ') + B.Height.String

#> BottomRight of A: 30, 15. Width and Height of B: 5, 10
