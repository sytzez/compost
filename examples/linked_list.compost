mod LinkedList
    class
        prev: Self | ?
    traits
        Prev: Self
        First: Self
        Last: Self
        Length: Int
        NodeString: String # A representation of the current node as a string.
        Reverse: Self
        Unshift: Self
        Concat: (other: Self) -> Self
        Print: String
    defs
        Last: Self
        First
            match prev: .Prev
                Self: prev.First
                ?: Self
        Length
            match prev: .Prev
                Self: prev.Length + 1
                ?: 1
        Print
            match prev: .Prev
                Self: prev.Print + ', ' + .NodeString
                ?: .NodeString

mod NumberList
    class
        value: Int
        prev: Self | ?
    traits
        Value: Int
        Push: (pushed: Int) -> Self
        Sum: Int
    defs
        Value: value
        Push
            NumberList
                value: pushed
                prev: Self
        Sum
            match prev: .Prev
                Self: prev.Sum + .Value
                ?: .Value
        LinkedList\Prev: prev
        LinkedList\NodeString: .Value.String
        LinkedList\Unshift
            match prev: .Prev
                Self
                    match prevPrev: prev.Prev
                        Self
                            NumberList
                                value: .Value
                                prev: .Prev.Unshift
                        ?
                            NumberList
                                value: .Value
                                prev: ?
                ?
                    NumberList
                        value: .Value
                        prev: ?
        LinkedList\Reverse
            match prev: .Prev
                Self
                    NumberList
                        value: .First.Value
                        prev: .Unshift.Reverse
                ?
                    Self
        LinkedList\Concat
            match prev: other.Prev
                Self
                    .Push(pushed: other.First.Value)
                    .Concat(other: other.Unshift)
                ?
                    .Push(pushed: other.Value)


lets
    MyNumberList: NumberList
        NumberList(value: 1, prev: ?)
        .Push(pushed: 2)
        .Push(pushed: 3)
        .Push(pushed: 4)

    Reversed: NumberList
        MyNumberList.Reverse

    Together: NumberList
        MyNumberList.Concat(other: Reversed)

    Main: String
        MyNumberList.Print
        + ' (total: ' + MyNumberList.Sum.String + ')'
        + '. Reversed: ' + Reversed.Print
        + ' (total: ' + Reversed.Sum.String + ')'
        + '. Together: ' + Together.Print
        + ' (total: ' + Together.Sum.String + ')'

#> 1, 2, 3, 4 (total: 10). Reversed: 4, 3, 2, 1 (total: 10). Together: 1, 2, 3, 4, 4, 3, 2, 1 (total: 20)
