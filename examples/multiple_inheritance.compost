#########################
#                       #
#      Animal      Egg  #
#     /      \      .   #
#    /        \    .    #
#  Mammal   Amphibian   #
#    \        /         #
#     \      /          #
#     Platypus          #
#                       #
#########################

mod Animal
    traits
        Name: String
        SpeciesName: String
        ChildsName: String
    defs
        ChildsName: 'Child of ' + .Name

mod Mammal
    using(Animal\*)
    traits
        RegulateBodyTemperature: String
    defs
        Mammal\RegulateBodyTemperature: 'Regulating...'

mod Amphibian
    using(Animal\*)
    traits(LayEgg: Egg)

mod Egg
    class(embryo: Amphibian)
    traits(Hatch: Amphibian)
    defs(Hatch: embryo)

mod Platypus
    using
        Mammal\*
        Amphibian\*
    class
        name: String
    defs
        Animal\Name: name
        Animal\SpeciesName: 'Platypus'
        Amphibian\LayEgg
            Egg
                embryo: Platypus(name: .ChildsName)

lets
    FullInformation: (animal: Animal) -> String
        animal.Name + ' (species: ' + animal.SpeciesName + ')'

    MyPlatypus: Platypus
        Platypus(name: 'Perry')

    Main: String
        FullInformation
            animal: MyPlatypus.LayEgg.Hatch

#> Child of Perry (species: Platypus)
